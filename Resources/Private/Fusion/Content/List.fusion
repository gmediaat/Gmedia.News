# News list
prototype(Gmedia.News:List) < prototype(Neos.Neos:ContentComponent) {

	# newsFolder is read from current node's newsFolder property if exists
	# otherwise the closest Folder node is selected
	@context.newsFolderNode = ${q(node).property('newsFolder') != null ? q(node).property('newsFolder') : q(node).closest('[instanceof Gmedia.News:Folder]')}

	# configuration merged from Settings and from the node properties.
	# this way configuration can be changed globally for all sites in Settings.yaml OR
	# change configuration in your Site package's TypoScript by overriding the values OR
	# if you set a new value for the configuration in the node property then it is only available
	# for particular view. in this case if you do any change in Settings or TypoScript level
	# then it will still take the configuration value from the node properties.
	@context.configuration = ${Configuration.setting('Gmedia.News.view.list')}
	@context.configuration.@process.mergeConfiguration = ${NewsConfiguration.mergeWithNodeProperties(value, node)}
	configuration = ${configuration}

	# TODO - remove the condition if Content
	newsCollection = ${q(newsFolderNode).children('[instanceof Gmedia.News:News]').get()}

	# Filter and sorting
	#@override.filterByCategories = ${q(node).is('[instanceof Gmedia.News:List]') ? q(node).property_array('filterByCategories') != null ? Array.join(q(node).property_array('filterByCategories')) : '' : ''}
	@context.filterByTag = ${request.arguments.tag}
	@context.filterByYear = ${request.arguments.year}
	@context.filterByMonth = ${request.arguments.month}
    @context.currentPaginationPage = ${request.arguments.page ? String.toInteger(request.arguments.page) : 1}

	# 1. filter the news collection by one or many categories if applicable
	# this internally uses the extended filter() operation
	newsCollection.@process.filterByCategories = ${filterByCategories != null && filterByCategories != '' ? q(value).filter('[categories *= \'' + filterByCategories + '\']').get() : value}

	# 2. filter the news collection by given year if applicable
	newsCollection.@process.filterByYear = ${filterByYear != null && filterByMonth == null ? q(value).filter('[dateTime = \'' + filterByYear + '\']', 'Y').get() : value}

	# 3. filter the news collection by given year and month if applicable
	newsCollection.@process.filterByYearAndMonth = ${filterByYear != null && filterByMonth != null ? q(value).filter('[dateTime = \'' + filterByYear + '/' + filterByMonth + '\']', 'Y/m').get() : value}

	# 4. filter the news collection by a tag if applicable
	newsCollection.@process.filterByTag = ${filterByTag != null ? q(value).filter('[tags *= ' + filterByTag + ']').get() : value}

	# sort nodes
    newsCollection.@process.sort = ${q(value).count() > 0 ? q(value).sort(configuration.sortProperty, configuration.sortOrder).get() : value}

	paginationEnd = ${Math.floor(q(this.newsCollection).count() / configuration.pagination.itemsPerPage) + 1}
	debug = ${((currentPaginationPage - 1)  * configuration.pagination.itemsPerPage)}
	// newsCollection.@process.pagination = ${configuration.pagination.itemsPerPage > 0 ? q(value).slice((1 - currentPaginationPage ) * configuration.pagination.itemsPerPage, configuration.pagination.itemsPerPage) : value}
	// newsCollection.@process.pagination = ${configuration.pagination.itemsPerPage > 0 ? q(value).slice((currentPaginationPage - 1) * configuration.pagination.itemsPerPage, configuration.pagination.itemsPerPage).get() : value}

	renderer = Neos.Fusion:Renderer {
		type = 'Gmedia.News:Component.List'
		element.newsCollection = ${props.newsCollection}
		element.configuration = ${props.configuration}
	}

	# uncached the list view
	@cache {
		mode = 'uncached'
		context {
			1 = 'node'
			2 = 'documentNode'
		}
	}
}
